# All-in-one Server Demo
#
# - HTTP Request Handler
# - Basic Session Handler
# - Database Object (MariaDB)
# - CURL
# - JSON
# - Encryption
#
# SQL Example:
# CREATE DATABASE xobase;
# CREATE USER 'xo'@'localhost' IDENTIFIED BY 'pass123';
# GRANT ALL PRIVILEGES ON xobase.* TO 'xo'@'localhost';
# FLUSH PRIVILEGES;
# ALTER DATABASE xobase CHARACTER SET utf8mb4 COLLATE utf8mb4_unicode_520_ci;
# CREATE TABLE book ( id INT PRIMARY KEY AUTO_INCREMENT, description VARCHAR(255), price DECIMAL(10,2), rating INT(10) ) CHARACTER SET utf8mb4 COLLATE utf8mb4_unicode_520_ci;

Program memory-management: 4.

# ~ HTTP Requests ~
Server init.
>> rq := HTTP-Request new.
Out write: ['HTTP-GET a='] + ( rq get: ['a'] ), stop. # GET
Out write: ['HTTP-POST c='] + ( rq post: ['c'] ), stop. # POST


# ~ Database ~
>> db := Maria-DB new host: ['localhost'],
	username: ['xo'], password: ['pass123'], dbname: ['xobase'], connect.

>> query := db query: ['
	INSERT INTO book (id, description, price, rating) 
	VALUES (NULL, ?, ?, ?)
'], parameters: ( List new ; ['abc'] ; 12.35 ; 6 ). # parameterized queries

{  query execute. db commit. } except: { :e db rollback.  }, start. # transactions
Out write: ['INSERTED: '] + query insert-id, stop. # insert id

>> query :=
db query: ['SELECT * FROM book order by id desc limit 2'], 
fetch: { :row Out write: row id + [' '] + row description + [': '] + row price, stop. }. # fetching rows

# ~ CURL ~
Out write: ( Net post: None to: ['https://redbeanphp.com'] ) length, stop. # post none = GET

# ~ JSON ~
>> json := (JSON jsonify: (Dict new put: (List new ; 1 ; 2 ; 3) at: ['abc'])).
Out write: json, stop.
Out write: ((JSON object: json) abc ? 1), stop.

# ~ Encryption ~
>> vault := Vault new: ['Encryptor'].
Out write: ['encrypt:'], stop.
>> x := Vault encrypt: ['hello'] key: ['world'].
Out write: x, stop.
Out write: ['decrypt:'], stop.
Out write: (Vault decrypt: x key: ['world']), stop.


# ~ Session handler (by default stored in /tmp) ~
Program use: ['webtools.xo'].
>> web := Web-Document new.
>> x := web session-start.
Out write: x, stop.
x user: 123.
web session-destroy.
web goto: ['/'].

# ~ Template ~
Program use: ['template.xo'].
>> html := ['
	<!-- cut:garbage -->
	<trash>
	<!-- /cut:garbage -->
	
	<p>
		
		<!-- cut:greeting -->
			<b><!-- slot:greeting --></b>
		<!-- /cut:greeting -->
		<!-- paste:greetings -->
		
		
    </p>
    
    <!-- cut:unused -->
    <span>remove this</span>
		<!-- cut:also -->
		<also>
		<!-- /cut:also -->
    <!-- /cut:unused -->
'].

>> tpl := Template new content: html.
>> greeting := tpl cut: ['greeting'], copy greeting: ['Hello World!'].
tpl paste: greeting at: ['greetings'].
web out: tpl clean-all.

# ~ Regex ~
>> subject := ['<html>
	<a href="http://link1" target="_blank">
	<a href="http://link2" target="_blank">
</html>'].
>> pattern := Pattern new: ['href="([^"]+)" target="([^"]+)"'].
>> result  := pattern match: subject do: { :m :x
	Out write: m, stop.
	Out write: x, stop.
	<- ['XXX'].
}.
Out write: result, stop.

# ~ Utilities ~
Out write: (Server html-encode: ['<&"' hello '"&>']), stop.
Out write: (Server url-encode: ['https://example.com?a=6&b=1 2']), stop.
Out write: (Format new: ['Number format: %06.0f \n'], apply: 56), stop.
Out write: (Vault token: 20), stop.
Out write: (Moment new format: ['%a, %d %b %Y %H:%M:%S']), stop.
>> a := Server base64-encode: ['abc123'].
Out write: a, stop.
Out write: (Server base64-decode: a), stop.


